allprojects {
    apply plugin: 'java'
    compileJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

apply plugin: 'idea'
apply plugin: 'maven'

group = project.property('group')
version = project.property('version')

def user = hasProperty('mavenUser') ? mavenUser : System.getenv('mavenUser')
def pass = hasProperty('mavenPassword') ? mavenPassword : System.getenv('mavenPassword')

repositories {
    mavenCentral()
    maven{
        name "Sonatype Public"
        url "http://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {

    compile (group: 'org.apache.commons', name: 'commons-lang3', version: 3.5)
    compile (group: 'commons-codec', name: 'commons-codec', version: 1.10)
    compile (group: 'org.yaml', name: 'snakeyaml', version: 1.17)
    testCompile (group: 'junit', name: 'junit', version: 4.12)

}

jar {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': 'com.drewhoener.Fido'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    //noinspection GroovyAssignabilityCheck
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {

            repository(url: "https://drewhoener.com/repo/repository/plugin-releases/") {
                authentication(userName: "$user", password: "$pass")
            }
            snapshotRepository(url: "https://drewhoener.com/repo/repository/plugin-snapshots/") {
                authentication(userName: "$user", password: "$pass")
            }
            pom.project {
                pom.version = project.version
                pom.artifactId = project.name
                pom.groupId = project.group
            }
        }
    }
}